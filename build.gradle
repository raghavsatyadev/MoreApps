buildscript {
    ext {
        APK_NAME = "MoreApps"

        kotlin_version = "1.7.20-Beta"

        TARGET_SDK = 33
        MIN_SDK = 19

        libraryVersion = "2.2.4"
        libraryVersionCode = 19

        renameOutputs = { variant, output, outputName ->
            outputFullName = "${outputName}-${variant.name}-${new Date().format('dd-MM-yy_HH-mm')}"
            if (!variant.getName().equals("debug")) {
                output.outputFileName = "../" + "${outputFullName}.apk"
            }
            if (variant.getName().equals('release')) {
                variant.assembleProvider.get().doLast {
                    outputFullName = "${outputName}-${variant.name}-${new Date().format('dd-MM-yy_HH-mm')}"
                    if (variant.mappingFileProvider.get() != null) {
                        copy {
                            from variant.mappingFileProvider.get()
                            into output.outputFile.parent
                            rename { String fileName ->
                                "${outputFullName}.txt"
                            }
                        }
                    }
                }
            }
        }
    }
    repositories {
        google()
//        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0-alpha09'
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.7.10"
    }
}
plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id "org.sonarqube" version "3.4.0.2513"
}
sonarqube {
    properties {
        property "sonar.projectKey", "MoreApps"
        property "sonar.organization", "raghavsatyadev"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply from: "${rootDir}/scripts/publish-root.gradle"
allprojects {
    repositories {
        google()
//        jcenter()
        mavenCentral()
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
