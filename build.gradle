buildscript {
    ext {
        APK_NAME = "MoreApps"

        kotlin_version = '1.8.0'
        dokka_version = "1.7.20"

        TARGET_SDK = 33
        MIN_SDK = 19

        libraryVersion = "2.2.9"
        libraryVersionCode = 23

        getAPKName = { variant ->
            return "${APK_NAME}-${variant.getName()}-${new Date().format("dd-MM-yy_HH-mm")}"
        }
        renameOutputs = { variant, output ->
            outputFullName = getAPKName(variant)
            if (!variant.getName().toLowerCase().contains("debug")) {
                output.outputFileName = "../" + "${outputFullName}.apk"
            }
            variant.assembleProvider.get().doLast {
                if (!variant.getName().toLowerCase().contains("debug")) {
                    if (variant.mappingFileProvider.get() != null) {
                        copy {
                            from variant.mappingFileProvider.get()
                            into output.outputFile.parent
                            rename { String fileName ->
                                "${getAPKName(variant)}.txt"
                            }
                        }
                    }
                }
            }
        }
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:8.1.0-alpha02')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}")
        classpath("com.google.gms:google-services:4.3.15")
    }
}
plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id "org.sonarqube" version "3.4.0.2513"
}
sonarqube {
    properties {
        property "sonar.projectKey", "MoreApps"
        property "sonar.organization", "raghavsatyadev"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply from: "${rootDir}/scripts/publish-root.gradle"
allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
tasks.register('clean') {
    delete rootProject.buildDir
}
