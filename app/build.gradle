plugins {
    id "com.android.application"

    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"

    id "com.google.gms.google-services"
}
def props = new Properties()
def credsFile = file("${rootProject.projectDir}/credentials.properties")
if (credsFile.exists()) {
    credsFile.withInputStream { props.load(it) }
}

android {
    compileSdk TARGET_SDK
    namespace "com.rocky.moreapps.example"
    defaultConfig {
        applicationId "com.rocky.moreapps.example"

        minSdk MIN_SDK
        targetSdk TARGET_SDK

        versionCode libraryVersionCode
        versionName libraryVersion

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        moreapps {
            if (credsFile.exists()) {
                storeFile file("${rootProject.projectDir}/../../keystore.jks")
                storePassword = props.getProperty("keystorePass")
                keyPassword = props.getProperty("keyPass")
                keyAlias = props.getProperty("keystoreAlias")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            versionNameSuffix = "-release"
            if (credsFile.exists()) {
                signingConfig signingConfigs.moreapps
            }
        }
        debug {
            versionNameSuffix = "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-Xjvm-default=all"
    }
    buildFeatures { buildConfig = true }
    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.all { output ->
            renameOutputs(variant, output)
        }
    }
}

dependencies {
    implementation project(path: ":moreapps")
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")

    implementation "androidx.appcompat:appcompat:1.7.0-alpha01"
    implementation 'com.google.android.material:material:1.9.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0-alpha06'
    implementation 'androidx.work:work-runtime:2.8.0-rc01'
//    implementation "io.github.raghavsatyadev:moreapps:${libraryVersion}"

    implementation "com.google.firebase:firebase-analytics-ktx:21.2.0"
    implementation "com.google.firebase:firebase-dynamic-links-ktx:21.1.0"
    implementation("androidx.multidex:multidex:2.0.1")
}