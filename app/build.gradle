plugins {
    id "com.android.application"

    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"
}
def props = new Properties()
def credsFile = file("${rootProject.projectDir}/credentials.properties")
if (credsFile.exists()) {
    credsFile.withInputStream { props.load(it) }
}

android {
    compileSdkVersion targetSdk
    defaultConfig {
        applicationId "com.rocky.moreapps.example"

        minSdkVersion minSdk
        targetSdkVersion targetSdk

        versionCode libraryVersionCode
        versionName libraryVersion

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }

    signingConfigs {
        moreapps {
            if (credsFile.exists()) {
                storeFile file("${rootProject.projectDir}/scripts/keystore.jks")
                storePassword = props.getProperty("keystorePass")
                keyPassword = props.getProperty("keyPass")
                keyAlias = props.getProperty("keystoreAlias")
            } else {
                storeFile file("${rootProject.projectDir}/scripts/keystore.jks")
                storePassword = System.getenv('KEYSTORE_PASS')
                keyPassword = System.getenv("KEY_PASS")
                keyAlias = System.getenv("KEYSTORE_ALIAS")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix = '-release'
            signingConfig signingConfigs.moreapps
        }
        debug {
            versionNameSuffix = '-debug'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            renameOutputs(variant, output, APK_NAME)
        }
    }
}

dependencies {
    implementation project(path: ':moreapps')
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")

    implementation 'androidx.appcompat:appcompat:1.6.0-alpha05'
    implementation 'com.google.android.material:material:1.7.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0-alpha03'
    implementation 'androidx.work:work-runtime:2.8.0-alpha02'
//    implementation "io.github.raghavsatyadev:moreapps:${libraryVersion}"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'
}